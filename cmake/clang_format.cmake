include_guard(GLOBAL)

find_program(
	JOSK_CLANG_FORMAT_BINARY
	NAMES "clang-format"
	DOC "Clang-format binary"
)

if (JOSK_CLANG_FORMAT_BINARY)
	set(JOSK_CLANG_FORMAT_OPTIONS
		${JOSK_CLANG_FORMAT_BINARY}
		--dry-run
	)

	if (CMAKE_COMPILE_WARNING_AS_ERROR)
		list(APPEND JOSK_CLANG_FORMAT_OPTIONS -Werror)
	endif ()

	file(GLOB_RECURSE JOSK_CLANG_FORMAT_FILES ${PROJECT_SOURCE_DIR}/src/*.cpp ${PROJECT_SOURCE_DIR}/src/*.hpp ${PROJECT_SOURCE_DIR}/tests/*.cpp ${PROJECT_SOURCE_DIR}/tests/*.hpp)
	list(APPEND JOSK_CLANG_FORMAT_OPTIONS ${JOSK_CLANG_FORMAT_FILES})

	set(JOSK_CLANG_FORMAT_TIMESTAMP_FILE ${CMAKE_CURRENT_BINARY_DIR}/clang_format_timestamp.txt)

	add_custom_command(OUTPUT ${JOSK_CLANG_FORMAT_TIMESTAMP_FILE}
		DEPENDS ${JOSK_CLANG_FORMAT_FILES}
		COMMAND ${JOSK_CLANG_FORMAT_OPTIONS}
		COMMAND ${CMAKE_COMMAND} -E touch ${JOSK_CLANG_FORMAT_TIMESTAMP_FILE}
		WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}
		COMMENT "Checking code with clang-format")

	add_custom_target(josk_clang_format ALL DEPENDS ${JOSK_CLANG_FORMAT_TIMESTAMP_FILE})

endif ()


